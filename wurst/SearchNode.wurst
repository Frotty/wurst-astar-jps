package SearchNode
import Assets
import HashMap

let map = new HashMap<int, effect>

public interface Hashable
    function hash() returns int

public class SearchNode implements Hashable
    vec2 pos
    SearchNode parent
    real h = 0.
    real g = 0.
    texttag tt

    var pruned = false
    real scale = 0.6
    var debug = false

    construct(vec2 pos, SearchNode parent, boolean debug)
        this.pos = pos
        this.parent = parent
        this.debug = debug

    ondestroy
        if debug and map.has(hash())
            map.getAndRemove(hash()).destr()
        if tt != null
            tt.destr()

    function setOpen()
        if debug
            if not map.has(hash())
                map.put(hash(), addEffect(Abilities.vengeanceMissile, pos))
                tt = createTTEx(pos.toVec3(), f().toString(0), 5.)
            map.get(hash()).setColor(0, 0, 255)

    function setClosed()
        if debug
            map.get(hash()).setColor(255, 0, 0)
        tt.destr()
        tt = null

    function setFinish()
        if debug
            map.get(hash()).setColor(0, 255, 0)

    function setActive()
        if debug
            map.get(hash()).setColor(55, 255, 55)

    override function hash() returns int
        return (pos.x.toInt() * 64 + pos.y.toInt())

    function setH(real h)
        this.h = h
        tt.setText(f().toString(0), 5)

    function setG(real g)
        this.g = g
        tt.setText(f().toString(0), 5)

    function h() returns real
        return h

    function g() returns real
        return g

    function f() returns real
        return h() + g()

    function isPruned() returns boolean
        return pruned

    function setPruned()
        pruned = true
